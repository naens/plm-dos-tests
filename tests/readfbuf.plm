readfile:
do;

$include('sys.ext')
$include('cons.ext')
$include('fio.ext')
$include('const.lit')

    /* test with bufsz=32: 19B, 32B, 48B, 64B, 72B */

    declare argc word;
    declare pargv address;
    declare argv based pargv address;
    declare pname address;
    declare name based pname address;
    declare line$len literally '10';
    declare str(line$len) byte;

    declare f word;
    declare n word;
    declare i word;
    declare c byte;

    call getargs(.argc, .pargv);
    call prstr(.('Opening the file ', 0));
    pname = pargv + 2;
    call prstr(name);
    call prcrlf;

    f = fopen(name, true, false);
    if f <> 0ffffh then
    do;
        call println(.('File open OK!', 0));
        n = line$len;

        do while n = line$len;
            n = freadbuf(f, .str, line$len);
            call prhexword(n);
            call prchr('|');
            do i = 1 to n;
                c = str(i - 1);
                if c <= ' ' or c > '~' then
                    call prchr('_');
                else
                    call prchr(c);
            end;

            call prchr('|');
            call prcrlf;
        end;

        call prstr(.('EOF', 0dh, 0ah, 0));
        call fclose(f);
        call println(.('File close OK!', 0));
    end;
    else
    do;
        call println(.('File open ERROR.', 0));
    end;

    call term;

end readfile;
